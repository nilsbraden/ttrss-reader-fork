/*
 * Copyright (c) 2015, Nils Braden
 *
 * This file is part of ttrss-reader-fork. This program is free software; you
 * can redistribute it and/or modify it under the terms of the GNU
 * General Public License as published by the Free Software Foundation;
 * either version 3 of the License, or (at your option) any later
 * version.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * General Public License for more details. You should have received a
 * copy of the GNU General Public License along with this program; If
 * not, see http://www.gnu.org/licenses/.
 */
apply plugin: 'com.android.application'

android {
    namespace 'org.ttrssreader'
    compileSdk 34

    defaultConfig {
        applicationId 'org.ttrssreader'
        minSdkVersion 19
        targetSdkVersion 35
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.txt']
        }
    }

    if (file('../signingconfig.gradle').exists()) {
        apply from: '../signingconfig.gradle'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    flavorDimensions "version"
    productFlavors {
        play {
            dimension "version"
        }
        fdroid {
            dimension "version"
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.antlr:ST4:4.3.4'
    implementation 'com.android.support:multidex:1.0.3'

    // Tasker Locale API: https://github.com/twofortyfouram/android-toast-setting-plugin-for-locale/blob/master/gradle.properties
    // implementation 'com.twofortyfouram:android-plugin-host-sdk-for-locale:9.0.0'
    implementation 'com.twofortyfouram:android-annotation:9.0.0'
    implementation 'com.twofortyfouram:android-assertion:9.0.0'
    implementation 'com.twofortyfouram:android-plugin-api-for-locale:9.0.0'
    implementation 'com.twofortyfouram:android-plugin-client-sdk-for-locale:9.0.0'
    implementation 'com.twofortyfouram:android-spackle:9.0.0'

    // Used to get ALT-Text of images from html
    implementation 'net.sourceforge.htmlcleaner:htmlcleaner:2.29'

    // Use AndroidX Preference implementation
    implementation 'androidx.preference:preference:1.2.1'

    // Remove any potentially harmful script-tags from incoming content
    implementation 'org.jsoup:jsoup:1.16.1'

    // "Updating Your Security Provider to Protect Against SSL Exploits"
    // https://developer.android.com/training/articles/security-gms-provider.html
    // Only used in productFlavour "play"
    playImplementation 'com.google.android.gms:play-services-base:18.5.0'

    // https://firebaseopensource.com/projects/googlesamples/easypermissions/
    implementation 'pub.devrel:easypermissions:3.0.0'

    // https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.12.0'

    // OkHttp https://square.github.io/okhttp/
    implementation 'com.squareup.okhttp3:okhttp:3.12.12'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'

    // Strange duplicate dependency error:
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.8.5'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.8.5'

    // Fix duplicate class errors
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.9.10"))

    // F*** Google, lots of difficulties with permissions for starting services...
    implementation 'androidx.work:work-runtime:2.9.1'
    implementation 'com.google.guava:guava:27.0.1-android'
}
