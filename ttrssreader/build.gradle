/*
 * Copyright (c) 2015, Nils Braden
 *
 * This file is part of ttrss-reader-fork. This program is free software; you
 * can redistribute it and/or modify it under the terms of the GNU
 * General Public License as published by the Free Software Foundation;
 * either version 3 of the License, or (at your option) any later
 * version.
 *
 * This program is distributed in the hope that it will be useful, but
 * WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU
 * General Public License for more details. You should have received a
 * copy of the GNU General Public License along with this program; If
 * not, see http://www.gnu.org/licenses/.
 */
apply plugin: 'com.android.application'

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId 'org.ttrssreader'
        minSdkVersion 16
        targetSdkVersion 30
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }

    if (file('../signingconfig.gradle').exists()) {
        apply from: '../signingconfig.gradle'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions "version"
    productFlavors {
        play {
            dimension "version"
        }
        fdroid {
            dimension "version"
        }
    }

    lintOptions {
        // checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation group: 'com.twofortyfouram', name: 'android-plugin-client-sdk-for-locale', version: '[4.0.3, 5.0['
    implementation 'com.google.code.gson:gson:2.8.8'
    implementation 'org.antlr:ST4:4.3'

    //Don't bump to 2.24 because of error in Utils.isEmptyString, based on wrong Flags for java.util.regex.Pattern
    // -> java.lang.IllegalArgumentException: Unsupported flags: 256
    implementation 'net.sourceforge.htmlcleaner:htmlcleaner:2.5'

    // Use AndroidX Preference implementation
    implementation 'androidx.preference:preference:1.1.1'

    // Remove any potentially harmful script-tags from incoming content
    implementation 'org.jsoup:jsoup:1.14.2'

    // "Updating Your Security Provider to Protect Against SSL Exploits"
    // https://developer.android.com/training/articles/security-gms-provider.html
    // Only used in productFlavour "play"
    playImplementation 'com.google.android.gms:play-services-base:17.6.0'

    // https://firebaseopensource.com/projects/googlesamples/easypermissions/
    implementation 'pub.devrel:easypermissions:3.0.0'

    // https://github.com/material-components/material-components-android/releases
    implementation 'com.google.android.material:material:1.4.0'

    // OkHttp https://square.github.io/okhttp/
    implementation("com.squareup.okhttp3:okhttp") {
        version {
            strictly '3.12.12'
        }
    }
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.12'
}
